buildscript {
  project.apply from: "${rootDir}/constants.gradle"
  project.apply from: "${rootDir}/android/constants.gradle"

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"
    classpath "com.android.tools.build:gradle:3.3.0"
    classpath "com.github.dcendents:android-maven-gradle-plugin:$androidMavenGradle"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokka"
  } 
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

repositories {
  google()
  jcenter()
  maven { url = "https://jitpack.io" }
}

allprojects {
  apply plugin: "org.jetbrains.dokka-android"
}

subprojects {
  repositories {
    google()
    jcenter()
    maven { url = "https://jitpack.io" }
  }
  
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'
  apply plugin: 'com.github.dcendents.android-maven'

  project.apply from: "${rootDir}/constants.gradle"
  project.apply from: "${rootDir}/android/constants.gradle"
  group = 'com.github.protoman92'
  version '1.0-SNAPSHOT'

  configurations {
    testArtifacts
  }

  task testJar(type: Jar, dependsOn: "assembleDebugUnitTest") {
    classifier = 'test'
    from "$project.buildDir/tmp/kotlin-classes/debugUnitTest"
  }

  artifacts {
    testArtifacts testJar
  }

  android {
    compileSdkVersion compileSdk

    defaultConfig {
      minSdkVersion minSdk
      targetSdkVersion targetSdk
      versionCode versionCode
      versionName versionName

      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
      release {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
    }
  }

  dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin"
    testImplementation "junit:junit:$junit"
    testImplementation "androidx.test:runner:$testRunner"
    testImplementation "androidx.test.ext:junit:$androidxJunit"
    testImplementation "org.robolectric:robolectric:$robolectric"
    androidTestImplementation "androidx.test:runner:$testRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunit"
  }
}

project(':android:android-util') {}

project(':android:android-ui') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    api project(':android:android-util')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation project (path: ":common:common-ui", configuration: 'testArtifacts')
  }
}

project(':android:android-lifecycle') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    implementation "androidx.appcompat:appcompat:$appCompat"
  }
}

project(':android:android-recyclerview') {
  dependencies {
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
    testImplementation project (path: ":android:android-lifecycle", configuration: 'testArtifacts')
  }
}

project(':android:android-rx-saga') {
  dependencies {
    api project(':common:common-saga')
    api project(':common:common-rx-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
  }
}

project(':android:android-livedata-rx-saga') {
  dependencies {
    api project(':common:common-saga')
    api project(':common:common-rx-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutines"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    testImplementation "androidx.arch.core:core-testing:$androidxCoreTest"
  }
}

project(':android:android-router') {
  dependencies {
    api project(':common:common-core')
    api project(':android:android-util')
    implementation "androidx.appcompat:appcompat:$appCompat"
  }
}

project(':android:android-all') {
  dependencies {
    api project(':common:common-all')
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    api project(':android:android-recyclerview')
    api project(':android:android-router')
    api project(':android:android-rx-saga')
    api project(':android:android-livedata-rx-saga')
  }
}
