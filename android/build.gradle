buildscript {
  def rootAbsolutePath = projectDir.parent
  project.apply from: "$rootAbsolutePath/constants.gradle"
  project.apply from: "$rootAbsolutePath/android/constants.gradle"

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.ext.kotlin"
    classpath "com.android.tools.build:gradle:$project.ext.gradle"
    classpath "com.github.dcendents:android-maven-gradle-plugin:$project.ext.androidMavenGradle"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$project.ext.dokka"
  } 
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

repositories {
  google()
  jcenter()
  maven { url = "https://jitpack.io" }
}

allprojects {
  apply plugin: "org.jetbrains.dokka-android"
}

def rootAbsolutePath = projectDir.parent

subprojects {
  repositories {
    google()
    jcenter()
    maven { url = "https://jitpack.io" }
  }
  
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'
  apply plugin: 'com.github.dcendents.android-maven'

  project.apply from: "$rootAbsolutePath/constants.gradle"
  project.apply from: "$rootAbsolutePath/android/constants.gradle"
  group = 'com.github.protoman92'
  version '1.0-SNAPSHOT'

  configurations {
    testArtifacts
  }

  task testJar(type: Jar, dependsOn: "assembleDebugUnitTest") {
    classifier = 'test'
    from "$project.buildDir/tmp/kotlin-classes/debugUnitTest"
  }

  artifacts {
    testArtifacts testJar
  }

  android {
    compileSdkVersion compileSdk

    defaultConfig {
      minSdkVersion minSdk
      targetSdkVersion targetSdk
      versionCode versionCode
      versionName versionName

      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
      release {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
    }
  }

  dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$project.ext.kotlin"
    testImplementation "junit:junit:$project.ext.junit"
    testImplementation "androidx.test:runner:$project.ext.testRunner"
    testImplementation "androidx.test.ext:junit:$project.ext.androidxJunit"
    testImplementation "org.robolectric:robolectric:$project.ext.robolectric"
    androidTestImplementation "androidx.test:runner:$project.ext.testRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$project.ext.espresso"
    androidTestImplementation "androidx.test.ext:junit:$project.ext.androidxJunit"
  }
}

project(':android:android-util') {}

project(':android:android-ui') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    api project(':android:android-util')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$project.ext.kotlinCoroutines"
    testImplementation project (path: ":common:common-ui", configuration: 'testArtifacts')
  }
}

project(':android:android-lifecycle') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    implementation "androidx.appcompat:appcompat:$appCompat"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project(':android:android-recyclerview') {
  dependencies {
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    implementation "androidx.lifecycle:lifecycle-runtime:$project.ext.lifecycle"
    implementation "androidx.recyclerview:recyclerview:$project.ext.recyclerView"
    testImplementation project (path: ":android:android-lifecycle", configuration: 'testArtifacts')
  }
}

project(':android:android-saga') {
  dependencies {
    api project(':common:common-saga')
    implementation "io.reactivex.rxjava2:rxjava:$project.ext.rxJava"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$project.ext.kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxkotlin:$project.ext.rxKotlin"
  }
}

project(':android:android-livedata-saga') {
  dependencies {
    api project(':common:common-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$project.ext.kotlinCoroutines"
    implementation "androidx.lifecycle:lifecycle-livedata:$project.ext.lifecycle"
    implementation "io.reactivex.rxjava2:rxjava:$project.ext.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$project.ext.rxAndroid"
    testImplementation "androidx.arch.core:core-testing:$project.ext.androidxCoreTest"
  }
}

project(':android:android-router') {
  dependencies {
    api project(':common:common-core')
    api project(':android:android-util')
    implementation "androidx.appcompat:appcompat:$appCompat"
  }
}

project(':android:android-all') {
  dependencies {
    api project(':common:common-all')
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    api project(':android:android-recyclerview')
    api project(':android:android-router')
    api project(':android:android-saga')
    api project(':android:android-livedata-saga')
  }
}
