buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.ext.kotlin"
  }
}

plugins {
  /** This is needed for publishing (e.g. jitpack.io */
  id 'maven-publish'
}

subprojects { thisProject ->
  apply plugin: 'kotlin'

  task packageTestJar(type: Jar) {
    archiveFileName.set("${thisProject.name}-test.jar")
    from(thisProject.sourceSets.test.output)
  }

  configurations {
    testArtifacts
  }

  artifacts {
    testArtifacts packageTestJar
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = thisProject.ext.jvmTarget
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = '1.8'
    }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    testImplementation "junit:junit:$junit"
  }

  /**
   * The group and version settings must be outside the publications block, otherwise the generated
   * POM files will not have the correct groupId and version.
   */
  afterEvaluate {
    group = 'com.github.protoman92.KotlinRedux'
    version = '1.0-SNAPSHOT'

    publishing {
      publications {
        "$thisProject.name"(MavenPublication) {
          groupId = thisProject.group
          version = thisProject.version
          artifactId = thisProject.name
          from components.kotlin
        }
      }
    }
  }
}

project(':common:common-core') {
  dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation project(":common:common-thunk")
    testImplementation project(":common:common-saga")
    testImplementation "io.reactivex.rxjava2:rxjava:$rxJava"
  }
}

project(':common:common-thunk') {
  dependencies {
    implementation project(':common:common-core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation project (path: ":common:common-core", configuration: 'testArtifacts')
  }
}

project(':common:common-saga') {
  dependencies {
    api project(':common:common-core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxjava:$rxJava"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutines"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito"
    testImplementation project (path: ":common:common-core", configuration: 'testArtifacts')
  }
}

project(':common:common-ui') {
  dependencies {
    api project(':common:common-core')
  }
}

project(':common:common-all') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-thunk')
    api project(':common:common-saga')
    api project(':common:common-ui')
  }
}
