buildscript {
  project.apply from: "${rootDir}/constants.gradle"

  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka"
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

repositories {
  jcenter()
}

allprojects {
  apply plugin: "org.jetbrains.dokka"
}

subprojects {
  repositories {
    jcenter()
  }

  apply plugin: "maven"
  apply plugin: "kotlin"
  group = 'com.github.protoman92'
  version '1.0-SNAPSHOT'

  project.apply from: "${rootDir}/constants.gradle"

  configurations { testArtifacts.extendsFrom testRuntime }

  task testJar (type: Jar) {
    classifier "test"
    from sourceSets.test.output
  }

  artifacts {
    testArtifacts testJar
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = project.ext.jvmTarget
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = '1.8'
    }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    testImplementation "junit:junit:$junit"
  }
}

project(':common:common-core') {
  dependencies {
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project(':common:common-async') {
  dependencies {
    api project(':common:common-core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation project (path: ":common:common-core", configuration: 'testArtifacts')
  }
}

project(':common:common-thunk') {
  dependencies {
    api project(':common:common-core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation project (path: ":common:common-core", configuration: 'testArtifacts')
  }
}

project(':common:common-saga') {
  dependencies {
    api project(':common:common-core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito"
    testImplementation project (path: ":common:common-core", configuration: 'testArtifacts')
  }
}

project(':common:common-rx-saga') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
    testImplementation project (path: ":common:common-saga", configuration: 'testArtifacts')
  }
}

project(':common:common-ui') {
  dependencies {
    api project(':common:common-core')
  }
}

project(':common:common-all') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-async')
    api project(':common:common-saga')
    api project(':common:common-rx-saga')
    api project(':common:common-ui')
  }
}
